#version 460 core

#include <guard.glsl>
#include <scene.glsl>
#include <math.glsl>

// Wrapper data packing tetrahedron data [x, y, z, w]; 64 bytes under std430
struct Elem {
  mat3 inv; // Inverse of 3x3 matrix [x - w, y - w, z - w]
  vec3 sub; // Subtractive component w
};

// General layout rule declarations
layout(local_size_x = 16, local_size_y = 16) in;
layout(std430) buffer;
layout(std140) uniform;

// Buffer declarations
layout(binding = 0) uniform b_buff_unif {
  uint object_i;
  uvec2 dispatch_n;
} unif;
layout(binding = 0) restrict readonly buffer b_buff_objects {
  ObjectInfo[] data;
} buff_objects;
layout(binding = 1) restrict readonly buffer b_buff_textures {
  TextureInfo[] data;
} buff_textures;
layout(binding = 2) restrict readonly buffer b_buff_uplifts {
  UpliftInfo data[];
} buff_uplifts;
layout(binding = 3) restrict readonly buffer b_buff_pack { 
  Elem data[]; 
} pack_buffer;

// Image/sampler declarations
layout(binding = 1) uniform sampler2DArray b_txtr_3f;
layout(binding = 2, rgba32f) uniform restrict writeonly image2DArray b_uplf_4f;

vec3 fetch_texel(in ObjectInfo object_info, in uvec2 tx_in) {
  if (object_info.is_albedo_sampled) {
    TextureInfo info = buff_textures.data[object_info.albedo_i];
    uvec2 tx = info.offs + min(tx_in, info.size - 1);
    return vec3(texelFetch(b_txtr_3f, ivec3(tx_in, info.layer), 0));
  } else {
    return object_info.albedo_v;
  }
}

void store_weight(in ObjectInfo info, in uvec2 tx_in, in vec4 bary) {
  uvec2 tx = info.offs + min(tx_in, info.size - 1);
  imageStore(b_uplf_4f, ivec3(tx, info.layer), bary);
}

// Shared memory declarations
shared Elem s_pack_in[512];

void load_shared(uint n) {
  for (uint i = gl_LocalInvocationID.x; i < n; i += gl_WorkGroupSize.x)
    s_pack_in[i] = pack_buffer.data[i];
  memoryBarrierShared();
  barrier();
}

void main() {
  // Load relevant object/uplifting data
  ObjectInfo object_info = buff_objects.data[unif.object_i];
  UpliftInfo uplift_info = buff_uplifts.data[object_info.uplifting_i];

  // Load pack data into shared memory
  load_shared(uplift_info.elem_size);

  // Cut off unused invocations
  const uvec2 i = gl_GlobalInvocationID.xy;
  guard(all(lessThan(i, unif.dispatch_n)));

  // Fetch the current texel
  vec3 p = fetch_texel(object_info, i).xyz;

  float result_err = FLT_MAX;
  vec4 result_bary;
  uint result_indx;

  for (uint j = 0; j < uplift_info.elem_size; ++j) {
    // Compute barycentric weights using packed element data
    vec3 xyz = s_pack_in[j].inv * (p - s_pack_in[j].sub);
    vec4 bary = vec4(xyz, 1.f - hsum(xyz));

    // Compute squared error of potentially unbounded barycentric weights
    float err = sdot(bary - clamp(bary, 0, 1));

    // Store better result if error is improved
    guard_continue(err < result_err);
    result_err  = err;
    result_bary = bary;
    result_indx = j;
  } // for (uint j)

  // Store result, packing 3/4th of weights, together with the tetrahedron's index
  store_weight(object_info, i, vec4(result_bary.xyz, float(result_indx)));
}