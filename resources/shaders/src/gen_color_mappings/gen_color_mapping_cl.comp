#version 460 core

// Enable necessary subgroup extensions
#extension GL_KHR_shader_subgroup_basic      : require
#extension GL_KHR_shader_subgroup_arithmetic : require
#extension GL_KHR_shader_subgroup_clustered  : require

// Set NVidia-CG flags
#pragma optionNV(fastmath on)
#pragma optionNV(inline   all)
#pragma optionNV(unroll   all)
#pragma optionNV(strict   on)

// Use shorthand for clustered types
#define Spec ClSpec
#define CMFS ClCMFS
#define Mapp ClMapp

#include <guard.glsl>
#include <mapping_invoc.glsl>
#include <mapping_cluster.glsl>

layout(local_size_x = 256) in;
layout(std430)             buffer;

layout(binding = 0) restrict readonly  buffer b_0 { InSpec data[]; } b_spec;
layout(binding = 1) restrict readonly  buffer b_1 { InMapp data[]; } b_mapp;
layout(binding = 2) restrict writeonly buffer b_2 { vec3   data[]; } b_colr;

layout(location = 0) uniform uint u_n;
layout(location = 1) uniform uint u_mapping_i;

shared InMapp s_mapping;

Spec load_spec(uint i) {
  Spec s;
  cl_spec_scatter(s, b_spec.data[i]);
  return s;
}

Mapp load_mapp_shared() {
  Mapp s;
  cl_mapp_scatter(s, s_mapping);
  return s;
}

void main() {
  const uint i =  gl_GlobalInvocationID.x / cl_spectrum_invc_n;
  guard(i < u_n);

  // Load mapping data into shared memory
  if (gl_LocalInvocationID.x == 0) {
    s_mapping = b_mapp.data[u_mapping_i];
  }
  memoryBarrierShared();
  barrier();

  // Load reflectance and mapping data into clustered format
  Spec sd = load_spec(i);
  Mapp mp = load_mapp_shared();

  // Compute and apply color mapping
  CMFS cs = finalize_mapp(mp, sd);
  vec3 v  = cl_mmul(cs, sd);

  // Sture results using first invocation 
  if (cl_bin_elect()) {
    b_colr.data[i] = v;
  }
}