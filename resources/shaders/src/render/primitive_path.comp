#include <preamble.glsl>
#include <guard.glsl>
#include <bvh.glsl>
#include <gbuffer.glsl>
#include <math.glsl>
#include <random_uniform.glsl>
#include <spectrum.glsl>
#include <scene.glsl>

// General layout rule declarations
layout(local_size_x = 16, local_size_y = 16) in;
layout(std140) uniform;
layout(std430) buffer;

// Uniform buffer declarations
layout(binding = 0) uniform b_buff_sensor {
  mat4  full_trf;
  mat4  proj_trf;
  mat4  view_trf;
  uvec2 film_size; 
} buff_sensor;
layout(binding = 1) uniform b_buff_sampler_state {
  uint spp_per_iter;
  uint spp_curr;
} buff_sampler_state;
layout(binding = 2) uniform b_buff_objects {
  uint n;
  ObjectInfo data[max_supported_objects];
} buff_objects;
layout(binding = 3) uniform b_buff_meshes {
  uint n;
  MeshInfo data[max_supported_meshes];
} buff_meshes;
layout(binding = 4) uniform b_buff_textures {
  TextureInfo data[max_supported_textures];
} buff_textures;
layout(binding = 5) uniform b_buff_emitters {
  uint n;
  EmitterInfo data[max_supported_objects];
} buff_emitters;

// Storage buffer declarations
layout(binding = 0) restrict buffer b_buff_sampler_data {
  uint data[];
} buff_sampler_data;
layout(binding = 1) restrict readonly buffer b_buff_bvhs_node {
  BVHNodePack data[];
} buff_bvhs_node;
layout(binding = 2) restrict readonly buffer b_buff_bvhs_prim {
  BVHPrimPack data[];
} buff_bvhs_prim;
layout(binding = 3) restrict readonly buffer b_buff_mesh_vert {
  BVHVertPack data[];
} buff_mesh_vert;
layout(binding = 4) restrict readonly buffer b_buff_mesh_elem {
  uvec3 data[];
} buff_mesh_elem;
layout(binding = 5) restrict readonly buffer b_buff_weights {
  AtlasLayout data[max_supported_objects];
} buff_weights;

// Texture unit, ergo sampler declarations
layout(binding = 0) uniform usampler2D     b_gbuffer; // G-buffer pack
layout(binding = 1) uniform sampler2DArray b_txtr_1f; // Alpha texture atlas
layout(binding = 2) uniform sampler2DArray b_txtr_3f; // RGB texture atlas
layout(binding = 3) uniform sampler2DArray b_bary_4f; // Uplifting weight atlas
layout(binding = 4) uniform sampler1DArray b_spec_4f; // Uplifting spectral atlas
layout(binding = 5) uniform sampler1DArray b_illm_1f; // Illuminant function data, 1 component
layout(binding = 6) uniform sampler1DArray b_cmfs_3f; // Observer function data, 3 components

// Image unit, output declaration
layout(binding = 0, rgba32f) uniform restrict image2D b_film;

// Shared memory declarations
shared ObjectInfo  s_objc_info[max_supported_objects];
shared EmitterInfo s_emtr_info[max_supported_objects];
shared MeshInfo    s_mesh_info[max_supported_meshes];
shared uint        s_stack[gl_WorkGroupSize.x * gl_WorkGroupSize.y][16];

// Define names of scene buffers to replace preproc in intersect.glsl
// to work around glsl's lack of ssbo argument passing
#define isct_n_objects      buff_objects.n
#define isct_n_emitters     buff_emitters.n
#define isct_stack          s_stack[gl_LocalInvocationID.y * gl_WorkGroupSize.x + gl_LocalInvocationID.x]
#define isct_buff_objc_info s_objc_info
#define isct_buff_emtr_info s_emtr_info
#define isct_buff_bvhs_info s_mesh_info
#define isct_buff_bvhs_node buff_bvhs_node.data
#define isct_buff_bvhs_prim buff_bvhs_prim.data
#include <intersect.glsl>
#include <emitter.glsl>

// Define names of scene buffers to replace preproc in surface.glsl
// to work around glsl's lack of ssbo argument passing
#define srfc_buff_prim      buff_bvhs_prim.data
#define srfc_buff_vert      buff_mesh_vert.data
#define srfc_buff_elem      buff_mesh_elem.data
#define srfc_buff_objc_info s_objc_info
#define srfc_buff_mesh_info s_mesh_info
#include <surface.glsl>

// Define names of scene buffers to replace preproc in brdf.glsl
// to work around glsl's lack of ssbo argument passing
#define brdf_buff_objc_info s_objc_info
#define brdf_buff_bary_info buff_weights.data
#define brdf_txtr_bary      b_bary_4f
#define brdf_txtr_spec      b_spec_4f
#include <brdf.glsl>

// Hardcoded value for now
vec3 hardcoded_lpos = vec3(6, 4, 2);

void load_shared() {
  for (uint i = gl_LocalInvocationID.x; i < buff_objects.n; i += gl_WorkGroupSize.x) {
    s_objc_info[i] = buff_objects.data[i];
  }
  for (uint i = gl_LocalInvocationID.x; i < buff_meshes.n; i += gl_WorkGroupSize.x) {
    s_mesh_info[i] = buff_meshes.data[i];
  }
  for (uint i = gl_LocalInvocationID.x; i < buff_emitters.n; i += gl_WorkGroupSize.x) {
    s_emtr_info[i] = buff_emitters.data[i];
  }
  memoryBarrierShared();
  barrier();
}

uint load_state() {
  // Initial sample state is seeded from pixel index, and rotated by multiplication
  uint invoc_state = 1 
                   + gl_GlobalInvocationID.x 
                   + gl_GlobalInvocationID.y * gl_NumWorkGroups.x * gl_WorkGroupSize.x;
  return invoc_state * (1 + buff_sampler_state.spp_curr);
}

void store_state(in uint state) {
  uint i = gl_GlobalInvocationID.y * buff_sensor.film_size.x + gl_GlobalInvocationID.x;
  buff_sampler_data.data[i] = state;
}

vec3 sample_colsys(in uint colsys_i, in float wvl) {
  // TODO sample by observer data instead
  return texture(b_cmfs_3f, vec2(wvl, colsys_i)).xyz/*  *
         texture(b_illm_1f, vec2(wvl, colsys_i)).x   * .01f */; // TODO fix integration through exposure
}

mat4x3 sample_colsys(in uint colsys_i, in vec4 wvl) {
  mat4x3 csys;
  for (uint i = 0; i < 4; ++i)
    csys[i] = sample_colsys(colsys_i, wvl[i]);
  return csys;
}

/* float eval_emitter(in uint emitter_i, in float wvl) {
  return texture(b_illm_1f, vec2(wvl, emitter_i)).x;
}

vec4 eval_emitter(in uint emitter_i, in vec4 wvl) {
  vec4 v;
  for (uint i = 0; i < 4; ++i)
    v[i] = eval_emitter(emitter_i, wvl[i]);
  return v;
} */

/* // Offset epsilon copied from mitsuba3
vec3 gen_offset_p(in SurfaceInfo si, in vec3 d) {
  float mag = 1.f + hmax(abs(si.p)) + 1500.f * M_EPS;
  mag = dot(si.n, d) >= 0 ? mag : -mag;
  return fma(vec3(mag), si.n, si.p);
} */

vec3 sample_emitter_position(inout uint state) {
  return hardcoded_lpos + next_3d(state) * 4.f - 2.f;
}

vec4 eval_env(in vec3 dir, in vec4 wvls) {
  return vec4(0); // vec4(max(0.f, dot(dir, normalize(vec3(0, 1, 0))))) * eval_emitter(0, wvls);
}

Ray ray_towards_direction(in SurfaceInfo si, in vec3 d) {
  Ray ray;

  ray.o = surface_offset(si, d);
  ray.d = d;
  ray.t = FLT_MAX;
  
  return ray;
}

Ray ray_towards_point(in SurfaceInfo si, in vec3 p) {
  Ray ray;

  ray.o = surface_offset(si, p - si.p);
  ray.d = p - ray.o;
  ray.t = length(ray.d);
  ray.d /= ray.t;
  // ray.t *= (1.f - M_RAY_EPS * 10.f);

  return ray;
}

Ray ray_from_sensor(in ivec2 i, in vec2 sample_2d) {
  // Get necessary sensor information
  float tan_y    = 1.f / buff_sensor.proj_trf[1][1];
  float aspect   = float(buff_sensor.film_size.x) / float(buff_sensor.film_size.y);
  mat4  view_inv = inverse(buff_sensor.view_trf);

  // Get pixel center in [-1, 1]
  // vec2 xy = (vec2(i) + 0.5) / vec2(buff_sensor.film_size);
  vec2 xy = (vec2(i) + sample_2d) / vec2(buff_sensor.film_size);
  xy = (xy - .5f) * 2.f;
  
  // Generate camera ray
  Ray ray;
  ray.o = (view_inv * vec4(0, 0, 0, 1)).xyz;
  ray.d = normalize((view_inv * vec4(xy.x * tan_y * aspect, xy.y * tan_y, -1, 0)).xyz);
  ray.t = FLT_MAX;

  return ray;
}

const float exposure  = 0.008f;
const uint  max_depth = 4;

float mis_balance(in float pdf_a, in float pdf_b) {
  return pdf_a / (pdf_a + pdf_b);
}

float mis_power(in float pdf_a, in float pdf_b) {
  pdf_a *= pdf_a;
  pdf_b *= pdf_b;
  return pdf_a / (pdf_a + pdf_b);
}

// Sample radiance along ray and wavelengths
vec4 Li(in Ray ray, in vec4 wvls, in uint state) {
  // Path throughput information; we track 4 wavelengths simultaneously
  // bool did_scatter = false; // TODO unused until emitters are added
  vec4 radiance   = vec4(0.f);
  vec4 throughput = vec4(1.f);

  // Iterate up to maximum depth
  for (uint depth = 0; depth < max_depth; ++depth) {
    /*  si;
    if (depth == 0) {
      uvec4 v = texelFetch(b_gbuffer, ivec2(gl_GlobalInvocationID.xy), 0);
      si = get_surface_info(unpack_gbuffer_ray(v), ray);
    } else { */
      
    // }

    // If the ray misses, terminate current path
    if (!ray_intersect_scene(ray)) {
      // if (depth > 0)
      //   radiance += throughput * eval_env(ray.d, wvls);
      break;
    }

    // If an emissive object is hit, terminate current path
    if (hit_emitter(ray)) {
      EmitterInfo em = s_emtr_info[ray_get_emitter(ray)];
      radiance = eval_emitter(em, wvls, vec3(0));
      break;
    }

    // If no surface object is visible, terminate current path
    SurfaceInfo si = get_surface_info(ray);
    if (!is_valid(si)) {
      break;
    }

    // Test if geometric/shading normals are aligned
    /* if (dot(ray.d, si.sh.n) * frame_cos_theta(si.wi) >= 0)
      break; */

    // Sample BRDF at position
    BRDF brdf = get_surface_brdf(si, wvls);
    
    // Emitter sampling
    {
      // Generate emitter sample position, and ray twards position
      EmitterInfo em = s_emtr_info[0];

      vec3  lpos = sample_emitter_position(state);
      Ray   lray = ray_towards_point(si, lpos);

      // If brdf has throughput and we are on the same side of a shading normal, evaluate hit
      vec4 f = eval_brdf(brdf, si, lray.d);
      if (!all(iszero(f)) && dot(lray.d, si.sh.n) > 0.f) {
        // If ray evaluates no hit, add contribution to path
        if (!ray_intersect_any(lray)) {
          vec4  Le   = eval_emitter(em, wvls, vec3(0));
          float lpdf = 1.f;
          float bpdf = pdf_brdf(brdf, si, lray.d);
          
          radiance += throughput * f * Le; // * mis_power(lpdf, bpdf);
        }
      }
    }

    // BRDF sampling; 
    {
      // If BRDF goes dark, terminate path
      BRDFSample bs = sample_brdf(brdf, next_2d(state), si);
      if (bs.pdf == 0.f)
        break;
      
      if (dot(si.sh.n, bs.wo) <= 0)
        break;
      
      // Update throughput
      throughput *= bs.f/*  / bs.pdf */;

      // Generate next scene ray
      ray = ray_towards_direction(si, bs.wo);
    }

    // TODO RR goes here
    // ...
  } // for (uint depth)

  return radiance;
}

void main() {
  load_shared();

  // 2D image coordinates; clamp invocations to image size
  const ivec2 i = ivec2(gl_GlobalInvocationID.xy);
  guard(all(lessThan(i, buff_sensor.film_size)));

  // Sampler state is based on 2d invocation index
  uint state = load_state();

  // Generate initial camera ray, sample ray's wavelengths
  Ray ray   = ray_from_sensor(i, next_2d(state));
  vec4 wvls = next_4d(state);

  /* { // TODO remove
    // ray_intersect_unit_sphere(ray);
    vec3 v = vec3(0);
    if (ray_intersect_scene(ray)) {
      v = ray.o + ray.d * ray.t; // Pos
    }

    // ray_intersect_scene(ray);
    // vec3 value = vec3(0);
    // if (is_valid(ray)) {
    //   uvec4 v = texelFetch(b_gbuffer, ivec2(gl_GlobalInvocationID.xy), 0);
    //   SurfaceInfo si = get_surface_info(unpack_gbuffer_ray(v), ray);
    //   // SurfaceInfo si = get_surface_info(ray);
    //   value = vec3((si.sh.n));
    // }
    imageStore(b_film, i, vec4(v, 1));
    return;
  } // TODO remove */

  // Evaluate ray/wavelength pair
  vec4 L = Li(ray, wvls, state) / vec4(1.f / wavelength_samples);

  // Integrate radiance over color system to get color
  vec3 value = sample_colsys(0, wvls) * exposure * L;
    
  // Reweight with prior accumulated radiance
  if (buff_sampler_state.spp_curr != 0) {
    value += imageLoad(b_film, ivec2(i)).xyz * float(buff_sampler_state.spp_curr);
    value /= float(buff_sampler_state.spp_curr + buff_sampler_state.spp_per_iter);
  }

  // Store updated image value and sample state
  imageStore(b_film, i, vec4(value, 1));
}